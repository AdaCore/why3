
(** {1 Arithmetic for programs} *)

(** {2 Integer Division}

It is checked that divisor is not null.

*)

module Int

  use export int.Int
  use export int.ComputerDivision

  let (/) (x: int) (y:int)
    requires { y <> 0 } ensures { result = div x y }
  = div x y

end

(** {2 Machine integers}

32-bit integers and such go here.

*)

module Int32

  use export int.Int

  function min_int : int = -2147483648
  function max_int : int =  2147483647

  let (+) (x: int) (y:int)
    requires { min_int <= x + y <= max_int } ensures { result = x + y }
  = x + y

  let (-) (x: int) (y:int)
    requires { min_int <= x - y <= max_int } ensures { result = x - y }
  = x - y

  let (-_) (x: int)
    requires { min_int <= -x <= max_int } ensures { result = -x }
  = -x

  let (*) (x: int) (y:int)
    requires { min_int <= x * y <= max_int } ensures { result = x * y }
  = x * y

  use export int.ComputerDivision

  let (/) (x: int) (y:int)
    requires { y <> 0 && min_int <= div x y <= max_int }
    ensures { result = div x y }
  = div x y

end

(** {2 Division on real numbers}

See also {h <a href="floating_point.why.html">Floating-Point theories</a>.}

*)

module Real

  use import real.Real
  use export real.RealInfix
  use export real.FromInt

  let (/.) (x: real) (y: real)
    requires { y <> 0. } ensures { result = x / y }
  = x / y

end
