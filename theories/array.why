
theory ArrayPoly

  type t 'a 'b

  logic get (t 'a 'b) 'a : 'b
  logic set (t 'a 'b) 'a 'b : t 'a 'b
  
  axiom Select_eq : 
    forall m : t 'a 'b. forall a1 a2 : 'a. 
    forall b : 'b [get (set m a1 b) a2].
    a1 = a2 -> get (set m a1 b) a2  = b

  axiom Select_neq : 
    forall m : t 'a 'b. forall a1 a2 : 'a. 
    forall b : 'b [get (set m a1 b) a2].
    a1 <> a2 -> get (set m a1 b) a2 = get m a2

  logic const 'b : t 'a 'b

  axiom Const : forall b:'b, a:'a. get (const b) a = b

end

theory Array

  type key

  type t 'a

  logic get (t 'a) key : 'a
  logic set (t 'a) key 'a : t 'a
  
  axiom Select_eq : 
    forall m : t 'a. forall a1 a2 : key. 
    forall b : 'a [get (set m a1 b) a2].
    a1 = a2 -> get (set m a1 b) a2 = b

  axiom Select_neq : 
    forall m : t 'a. forall a1 a2 : key. 
    forall b : 'a [get (set m a1 b) a2].
    a1 <> a2 -> get (set m a1 b) a2 = get m a2

  logic const 'a : t 'a

  axiom Const : forall b:'a. forall a:key. get (const b) a = b

end

theory ArrayLength

  clone export Array with type key = int

  logic length (t 'a) : int

  logic const_length 'a int : t 'a

  axiom Const_contents : 
    forall b:'a. forall n i:int. get (const_length b n) i = b

  axiom Length_const : 
    forall a : 'a. forall n : int. length (const_length a n) = n

  axiom Length_set : 
    forall a : t 'a. forall k : int. forall v : 'a.
    length (set a k v) = length a

end

theory ArrayRich

  use import int.Int
  
  clone export ArrayLength

  logic create int : t 'a

  axiom Create_length : 
    forall n : int. length (create n : t 'a) = n

  logic sub (t 'a) int int : t 'a

  axiom Sub_length : 
    forall s : t 'a, ofs len : int. length (sub s ofs len) = len

  axiom Sub_get :
    forall s : t 'a, ofs len i : int. 
    get (sub s ofs len) i = get s (ofs + i)

  logic app (t 'a) (t 'a) : t 'a

end

theory BitVector

  use export bool.Bool
  use import int.Int

  logic size : int 

  type bv

  logic nth bv int: bool
  
  logic bvzero : bv
  axiom Nth_zero: 
    forall n:int. 0 <= n < size -> nth bvzero n = False

  logic bvone : bv
  axiom Nth_one: 
    forall n:int. 0 <= n < size -> nth bvone n = True

  logic eq (v1 v2 : bv) =
    forall n:int. 0 <= n < size -> nth v1 n = nth v2 n

  logic bw_and (v1 v2 : bv) : bv
  axiom Nth_bw_and: 
    forall v1 v2:bv, n:int. 0 <= n < size -> 
      nth (bw_and v1 v2) n = andb (nth v1 n) (nth v2 n)

  (* logical shift right *)
  logic lsr bv int : bv

  axiom Lsr_nth_low:
    forall b:bv,n s:int. 0 <= n+s < size -> nth (lsr b s) n = nth b (n+s)

  axiom Lsr_nth_high:
    forall b:bv,n s:int. n+s >= size -> nth (lsr b s) n = False

  (* arithmetic shift left *)
  logic asr bv int : bv

  axiom Asr_nth_low:
    forall b:bv,n s:int. 0 <= n+s < size -> nth (asr b s) n = nth b (n+s)

  axiom Asr_nth_high:
    forall b:bv,n s:int. n+s >= size -> nth (asr b s) n = nth b (size-1)

  (* logical shift left *)
  logic lsl bv int : bv

  axiom Lsl_nth_high:
    forall b:bv,n s:int. 0 <= n-s < size -> nth (lsl b s) n = nth b (n-s)

  axiom Lsl_nth_low:
    forall b:bv,n s:int. n-s < 0 -> nth (lsl b s) n = False

end


theory BV32

  logic size : int = 32

  clone export BitVector with logic size = size

end


theory TestBv32

  use import BV32

  lemma Test1:
    let b = bw_and bvzero bvone in nth b 1 = False

  lemma Test2:
    let b = lsr bvone 16 in nth b 15 = True
     
  lemma Test3:
    let b = lsr bvone 16 in nth b 16 = False

  lemma Test4:
    let b = asr bvone 16 in nth b 15 = True
     
  lemma Test5:
    let b = asr bvone 16 in nth b 16 = True

  lemma Test6:
    let b = asr (lsr bvone 1) 16 in nth b 16 = False
     
end 


(*
Local Variables: 
compile-command: "cd ..; bin/why.opt theories/array.why"
End: 
*)
