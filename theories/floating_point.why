

(* definition of IEEE-754 rounding modes *)
theory Rounding

  type mode = NearestTiesToEven | ToZero | Up | Down | NearTiesToAway
    (** nearest ties to even, to zero, upward, downward, nearest ties to away *)

end


(* handling of IEEE-754 special values +\infty, -\infty, NaN, +0, -0 *)
theory SpecialValues

  type class = Finite | Infinite | NaN

  type sign = Neg | Pos

  use import real.Real

  inductive same_sign_real sign real =
    | Neg_case: forall x:real. x < 0.0 -> same_sign_real Neg x
    | Pos_case: forall x:real. x > 0.0 -> same_sign_real Pos x

end

theory GenFloat

  use import Rounding
  use import real.Real
  use import real.Abs
  use import real.FromInt
  use int.Int

  type t

  function round mode real : real
  function round_logic mode real : t

  function value t : real
  function exact t : real
  function model t : real

  function round_error (x : t) : real = abs (value x - exact x)
  function total_error (x : t) : real = abs (value x - model x)

  function max : real

  predicate no_overflow (m:mode) (x:real) = abs (round m x) <= max

  axiom Bounded_real_no_overflow :
    forall m:mode, x:real. abs x <= max -> no_overflow m x

  axiom Round_monotonic :
    forall m:mode, x y:real. x <= y -> round m x <= round m y

  axiom Round_idempotent :
    forall m1 m2:mode, x:real. round m1 (round m2 x) = round m2 x

  axiom Round_value :
    forall m:mode, x:t. round m (value x) = value x

  axiom Bounded_value :
    forall x:t. abs (value x) <= max

  function max_representable_integer : int

  axiom Exact_rounding_for_integers:
    forall m:mode,i:int.
      Int.(<=) (Int.(-_) max_representable_integer) i /\
      Int.(<=) i max_representable_integer ->
        round m (from_int i) = from_int i

  (* rounding up and down *)
  axiom Round_down_le:
    forall x:real. round Down x <= x
  axiom Round_up_ge:
    forall x:real. round Up x >= x
  axiom Round_down_neg:
    forall x:real. round Down (-x) = - round Up x
  axiom Round_up_neg:
    forall x:real. round Up (-x) = - round Down x

  use import SpecialValues

  (* special values *)
  function class t : class

  predicate is_finite (x:t) = class x = Finite
  predicate is_infinite (x:t) = class x = Infinite
  predicate is_NaN (x:t) = class x = NaN
  predicate is_not_NaN (x:t) = is_finite x \/ is_infinite x

  lemma is_not_NaN: forall x:t. is_not_NaN x <-> not (is_NaN x)

  function sign t  : sign

  predicate same_sign (x:t) (y:t)  = sign x = sign y

  predicate is_minus_infinity (x:t) = is_infinite x /\ sign x = Neg
  predicate is_plus_infinity (x:t) = is_infinite x /\ sign x = Pos
  predicate is_gen_zero (x:t) = is_finite x /\ value x = 0.0
  predicate is_gen_zero_plus (x:t) = is_gen_zero x /\ sign x = Pos
  predicate is_gen_zero_minus (x:t) = is_gen_zero x /\ sign x = Neg

  predicate le (x:t) (y:t) =
    (is_finite x /\ is_finite y /\ value x <= value y)
    \/ (is_minus_infinity x /\ is_not_NaN y)
    \/ (is_not_NaN x /\ is_plus_infinity y)

  predicate lt (x:t) (y:t) =
    (is_finite x /\ is_finite y /\ value x < value y)
    \/ (is_minus_infinity x /\ is_not_NaN y /\ not (is_minus_infinity y))
    \/ (is_not_NaN x /\ not (is_plus_infinity x) /\ is_plus_infinity y)

  predicate ge (x:t) (y:t) = le y x

  predicate gt (x:t) (y:t) = lt y x

  predicate eq (x:t) (y:t) =
    is_not_NaN x /\ is_not_NaN y /\
      ((is_finite x /\ is_finite y /\ value x = value y) \/
       (is_infinite x /\ is_infinite y /\ same_sign x y))

  predicate ne (x:t) (y:t) = not (eq x y)

  lemma le_lt_trans: forall x y z:t. le x y /\ lt y z -> lt x z

  lemma lt_le_trans: forall x y z:t. lt x y /\ le y z -> lt x z

  lemma le_ge_asym: forall x y:t. le x y /\ ge x y -> eq x y

  lemma not_lt_ge: forall x y:t.
    not (lt x y) /\ is_not_NaN x /\ is_not_NaN y -> ge x y

  lemma not_gt_le: forall x y:t.
    not (gt x y) /\ is_not_NaN x /\ is_not_NaN y -> le x y

end


theory Single

  type single

  function max_single : real = 0x1.FFFFFEp127
  function max_int : int = 16777216 (* 2^24 *)

  clone export GenFloat with
    type t = single,
    function max = max_single,
    function max_representable_integer = max_int

end



theory Double

  type double

  function max_double : real = 0x1.FFFFFFFFFFFFFp1023
  function max_int : int = 9007199254740992 (* 2^23 *)

  clone export GenFloat with
    type t = double,
    function max = max_double,
    function max_representable_integer = max_int

end


