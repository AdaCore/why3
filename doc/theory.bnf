\begin{syntax}
  theory ::= "theory" uident label* decl* "end" 
  \
  decl ::= "type" type-decl ("with" type-decl)* ;
    | "function" function-decl ("with" logic-decl)* ;
    | "predicate" predicate-decl ("with" logic-decl)* ;
    | "inductive" inductive-decl ("with" inductive-decl)* ;
    | "axiom" ident ":" formula 	   ;
    | "lemma" ident ":" formula 	   ;
    | "goal"  ident ":" formula 	   ;
    | "use" imp-exp tqualid ("as" uident-opt)?     ; 
    | "clone" imp-exp tqualid ("as" uident-opt)? subst? ;	   
    | "namespace" "import"? uident-opt decl* "end" ;
  \
  type-decl ::= lident label* ("'" lident label*)* type-defn;
  \
  type-defn ::=                                      ; abstract type
    | "=" type                                       ; alias type
    | "=" "|"? type-case ("|" type-case)*            ; algebraic type
    | "=" "{|" record-field (";" record-field)* "|}" ; record type
  \
  type-case ::= uident label* type-param*
  \
  record-field ::= lident label* ":" type
  \
  logic-decl ::= function-decl ;
    | predicate-decl
  \
  function-decl ::= lident label* type-param* ":" type ;
    | lident label* type-param* ":" type "=" term
  \
  predicate-decl ::= lident label* type-param* ;
    | lident label* type-param* "=" formula
  \
  type-param ::= "'" lident   ;
   | lqualid                  ;
   | "(" lident+ ":" type ")" ;
   | "(" type ("," type)* ")" ;
   | "()"
  \
  inductive-decl ::= lident label* type-param* "=" ;
                     "|"? ind-case ("|" ind-case)* ;
  \
  ind-case ::= ident label* ":" formula ;
  \
  imp-exp ::= ("import" | "export")?
  \
  uident-opt ::= uident | "_"
  \
  subst ::= "with" ("," subst-elt)+
  \
  subst-elt ::= "type" lqualid "=" lqualid ;
    | "function" lqualid "=" lqualid          ;
    | "predicate" lqualid "=" lqualid         ;
    | "namespace" (uqualid | ".") "=" (uqualid | ".")  ;
    | "lemma" uqualid 	  		   ;
    | "goal"  uqualid			   ;
  \
  tqualid ::= uident | ident ("." ident)* "." uident ; %
\end{syntax}
