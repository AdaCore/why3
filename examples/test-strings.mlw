
module Harness

  use newstring.String
  use int.Int

  goal Empty: "" = ""

  goal Empty2: empty = ""

  goal Test:
    let s = "abcdef" in substring s 2 3 = "cde"

  goal TestConcat2:
    let s1 = "abc" in
    let s2 = "defg" in
    concat2 s1 s2 = "abcdefg"

  goal TestConcat3:
    let s1 = "abc" in
    let s2 = "defg" in
    let s3 = "hij" in
    concat3 s1 s2 s3 = "abcdefghij"

  goal TestConcat4:
    let s1 = "abc" in
    let s2 = "defg" in
    let s3 = "hij" in
    let s4 = "klmnop" in
    concat4 s1 s2 s3 s4 = "abcdefghijklmnop"

  goal TestConcat5:
    let s1 = "abc" in
    let s2 = "defg" in
    let s3 = "hij" in
    let s4 = "klmnop" in
    let s5 = "qrst" in
    concat5 s1 s2 s3 s4 s5 = "abcdefghijklmnopqrst"

  goal TestConcat6:
    concat2 "" "" = ""

  goal TestLength1: length "" = 0
  goal TestLength2: length "a" = 1
  goal TestLength3: length "ab" = 2
  goal TestLength4: length "abc" = 3
  goal TestLength5: length (concat2 "ab" "12") = 4

  goal TestLength6: forall s1 s2.
    length (concat2 s1 s2) = length s1 + length s2

  goal TestIndefof1:
    indexof "" "" 0 = 0

  goal TestIndefof2:
    indexof "a" "" 0 = 0

  goal TestIndefof3:
    indexof "" "a" 0 = -1

  goal TestIndefof4: (* CHECK THIS *)
    indexof "a" "" 1 = 1

  goal TestIndefof5:
    indexof "a" "" 2 = -1

  goal TestIndefof6: (* CHECK THIS *)
    indexof "ab" "" 2 = 2

  goal TestIndefof7:
    indexof "abcdef" "c" 0 = 2

  goal TestIndefof8:
    indexof "abcdef" "c" 2 = 2

  goal TestIndefof9:
    indexof "abcdef" "c" 3 = -1

  goal TestIndefof10:
    indexof "abcdef" "cdef" 0 = 2

  goal TestIndexof11: forall s1.
    indexof s1 "" 0 = 0

  goal TestIndexof12: forall s2.
    s2 <> "" -> indexof "" s2 0 = -1

  goal TestIndexof13: (* check this *)
    indexof "abc" "c" (-1) = -1

  goal TestIndefof14:
    indexof "abcdefgh" "" 4 = 4

  goal TestIndefof15:
    indexof "ab" "" 4 = -1

  goal TestReplace1: forall s2 s3.
   s2 <> "" -> replace "" s2 s3 = ""

  goal TestReplace2: forall s1 s3. (* check standard to see if this makes sense *)
   replace s1 "" s3 = concat2 s3 s1

  goal TestReplace3:
   replace "abcde" "bc" "1234" = "a1234de"

  goal TestReplace4:
   replace "abcdefg" "fg" "" = "abcde"

  goal TestReplace5:
   replace "abcdefabcdef" "bc" "123" = "a123defabcdef"

  goal TestReplaceAll1: forall s1 s3. (* it has not the same behavior as replace *)
    s1 <> "" -> replaceall s1 "" s3 = concat2 s3 s1

  goal TestReplaceAll2:
    replaceall "abcdefabcdefabcdef" "de" "1234" = "abc1234fabc1234fabc1234f"

  goal TestSubstring1: forall i x.
    substring "" i x = ""

  goal TestSubstring2: forall s i x.
    i < 0 -> substring s i x = ""

  goal TestSubstring3: forall s i x.
    i > length s -> substring s i x = ""

  goal TestSubstring4:
    substring "abcdef" 1 3 = "bcd"

  goal TestSubstring5:
    substring "abcdef" 1 10 = "bcdef"


  lemma replace_substring_indexof: forall s t1 t2.
    let i = indexof s t1 0 in
    replace s t1 t2 =
      if i < 0 then s else
        concat3 (substring s 0 i)
                t2
                (substring s (i + length t1) (length s - i - length t1))

  lemma substring_indexof: forall s t i.
    let j = indexof s t i in
    j >= 0 && 0 <= i <= length s ->
      substring s j (length t) = t

  lemma x: forall s i.
    substring s i 0 = ""

  lemma y: forall s i.
    0 <= i <= length s -> indexof s "" i = i

  lemma z: forall i s t.
    not (0 <= i <= length s) -> indexof s t i = -1

  lemma w: forall i s t.
    0 <= i <= length s ->
      indexof s t i = -1 || i <= indexof s t i <= length s

  (* goal ConcatAssoc: forall s1 s2 s3. *)
  (*   concat2 s1 (concat2 s2 s3) = concat2 (concat2 s1 s2) s3 *)

end
