
(** Given an array a of size n, removes its duplicate elements,
    in-place, as follows: return r such that a[0..r[ contains the same
    elements as a[0..n[ and no duplicate *)

module Spec

  use export int.Int
  use export array.Array

  (** v appears in a[0..s[ *)
  predicate appears (v: 'a) (a: array 'a) (s: int) =
    exists i: int. 0 <= i < s /\ a[i] = v

  (** a[0..s[ contains no duplicate element *)
  predicate nodup (a: array 'a) (s: int) =
    forall i: int. 0 <= i < s -> not (appears a[i] a i)

end

(** Quadratic implementation, without extra space *)

module RemoveDuplicateQuadratic

  use import Spec
  use import ref.Refint

  let rec test_appears (v: 'a) (a: array 'a) (s: int) : bool
    requires { 0 <= s <= length a }
    ensures  { result=True <-> appears v a s }
    variant  { s }
  = s > 0 && (a[s-1] = v || test_appears v a (s-1))

  let remove_duplicate (a: array 'a) : int
    ensures { 0 <= result <= length a }
    ensures { nodup a result }
    ensures { forall v:'a. appears v (old a) (length a) <-> appears v a result }
  = let n = length a in
    let r = ref 0 in
    'L:
    for i = 0 to n - 1 do
      invariant { 0 <= !r <= i }
      invariant { nodup a !r }
      invariant { forall v: 'a. appears v (at a 'L) i <-> appears v a !r }
      invariant { forall j: int. i <= j < n -> a[j] = (at a 'L)[j] }
      if not (test_appears a[i] a !r) then begin a[!r] <- a[i]; incr r end
    done;
    !r


end
