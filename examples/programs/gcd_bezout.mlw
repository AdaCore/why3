
(* Greatest common divisor, with BÃ©zout coefficients *)

module GcdBezout

  use import int.Int
  use import int.ComputerDivision
  use import number.Gcd
  use import module ref.Ref

  let gcd (x:int) (y:int) =
    { x >= 0 /\ y >= 0 }
    let x = ref x in let y = ref y in
'Pre:
    let a = ref 1 in let b = ref 0 in
    let c = ref 0 in let d = ref 1 in
    while (!y > 0) do
       invariant { !x >= 0 /\ !y >= 0 /\
          gcd !x !y = gcd (at !x 'Pre) (at !y 'Pre) /\
          !a * (at !x 'Pre) + !b * (at !y 'Pre) = !x /\
          !c * (at !x 'Pre) + !d * (at !y 'Pre) = !y }
       variant { !y }
       let r = mod !x !y in let q = div !x !y in
       x := !y; y := r;
       let ta = !a in let tb = !b in
       a := !c; b := !d;
       c := ta - !c * q; d := tb - !d * q
    done;
    !x
    { result = gcd x y /\
      exists a b:int. a*x+b*y = result }

end

(*
Local Variables:
compile-command: "unset LANG; make -C ../.. examples/programs/gcd_bezout.gui"
End:
*)
