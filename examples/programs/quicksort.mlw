
(* Sorting an array using quicksort, with partitioning a la Bentley. 

   We simply scan the segment l..r from left ro right, maintaining values
   smaller than t[l] on the left part of it (in l+1..m).
   Then we swap t[l] and t[m] and we are done. *)

module Quicksort

  use import int.Int
  use import module ref.Ref
  use import module array.Array
  use import module array.ArraySorted
  use import module array.ArrayPermut
  use import module array.ArrayEq

  let swap (t:array int) (i:int) (j:int) =
    { 0 <= i < length t and 0 <= j < length t }
    let v = t[i] in
    begin
      set t i t[j];
      set t j v
    end
    { exchange t (old t) i j }

  let rec quick_rec (t:array int) (l:int) (r:int) : unit variant { 1+r-l } =
    { 0 <= l and r < length t }
    if l < r then begin 
      let v = t[l] in
      let m = ref l in
      label L: begin
      for i = l + 1 to r do
	invariant { (forall j:int. l < j <= m -> t[j] < v) and
                    (forall j:int. m < j <  i -> t[j] >= v) and
                    permut_sub t (at t L) l r and
                    t[l] = v and l <= m < i }
        if t[i] < v then begin m := !m + 1; swap t i !m end
      done;
      swap t l !m;
      quick_rec t l (!m - 1);
      quick_rec t (!m + 1) r
    end end
    { (l <= r and sorted_sub t l r and permut_sub t (old t) l r) or
      (l > r and array_eq t (old t)) }

  let quicksort (t : array int) =
    {} 
    quick_rec t 0 (length t - 1)
    { sorted t and permut t (old t) }

end

(*
Local Variables: 
compile-command: "unset LANG; make -C ../.. examples/programs/quicksort.gui"
End: 
*)

