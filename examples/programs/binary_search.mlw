module M

  use import int.Int
  use import int.ComputerDivision
  use import module stdlib.Ref
  use import module stdlib.Array

  (* Binary search

   The classical example. Searches a sorted array for a given value v. 
  *)

  (* the code and its specification *)

  exception Break int (* raised to exit the loop *)
  exception Not_found (* raised to signal a search failure *)

  let binary_search (a :array int) (v : int) =
    { (* forall i1 i2 : int. 0 <= i1 <= i2 < length a -> a[i1] <= a[i2] *) }
    try
      let l = ref 0 in
      let u = ref (length a - 1) in
      while !l <= !u do
        invariant {
	  0 <= l and u < length a and 
          forall i : int. 0 <= i < length a -> a[i] = v -> l <= i <= u }
        variant { u - l }
        let m = !l + div (!u - !l) 2 in
        assert { l <= m <= u };
        if a[m] < v then
          l := m + 1
        else if a[m] > v then
          u := m - 1
        else 
          raise (Break m)
      done;
      raise Not_found
    with Break i ->
      i
    end
    { 0 <= result < length a and a[result] = v }
    | Not_found -> { forall i:int. 0 <= i < length a -> a[i] <> v }

end

(*
Local Variables: 
compile-command: "unset LANG; make -C ../.. examples/programs/binary_search.gui"
End: 
*)

