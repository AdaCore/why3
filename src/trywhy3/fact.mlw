module FactWhile
  use import mach.int.Int
  use import int.Fact
  use import ref.Ref

  (** Factorial with a while loop *)
  let fact_imp (x:int) : int
    requires { x >= 0 }
    ensures { result = fact x }
  = let y = ref 0 in
    let r = ref 1 in
    while !y < x do
      invariant { 0 <= !y <= x }
      invariant { !r = fact !y }
      variant { x - !y }
      y := !y + 1;
      r := !r * !y
    done;
    !r

  let main () = (fact_imp 7, fact_imp 42)

end

module FactFor
  use import mach.int.Int
  use import int.Fact
  use import ref.Ref

  (** Factorial with a for loop *)
  let fact_imp (x:int) : int
    requires { x >= 0 }
    ensures { result = fact x }
  = let r = ref 1 in
    for y = 1 to x do
      invariant { !r = fact (y-1) }
      r := !r * !y
    done;
    !r

  let main () = (fact_imp 7, fact_imp 42)

end
